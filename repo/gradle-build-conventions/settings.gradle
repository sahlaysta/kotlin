pluginManagement {
    apply from: '../scripts/cache-redirector.settings.gradle.kts'
    apply from: '../scripts/kotlin-bootstrap.settings.gradle.kts'

    includeBuild '../gradle-settings-conventions'

    repositories {
        maven { url "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-dependencies" }
        mavenCentral()
        google()
        gradlePluginPortal()
    }
}

plugins {
    id "build-cache"
    id "gradle-enterprise"
    id "jvm-toolchain-provisioning"
    id "kotlin-daemon-config"
}

File versionPropertiesFile = new File(rootProject.projectDir.parentFile, "../gradle/versions.properties")
def versionProperties = new Properties()
versionPropertiesFile.withInputStream {
    versionProperties.load(it)
}
dependencyResolutionManagement {
    components {
        withModule("com.google.code.gson:gson") {
            allVariants {
                withDependencies {
                    add("com.google.code.gson:gson") {
                        version {
                            it.require(versionProperties['versions.gson'])
                        }
                        because("Force using same gson version because of https://github.com/google/gson/pull/1991")
                    }
                }
            }
        }
    }
}

include ":buildsrc-compat"
include ":prepare-deps"
